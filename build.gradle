buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name "forge"
            url "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${minecraft_version}-${mod_version}"
group = 'sonar.fluxnetworks'
archivesBaseName = 'FluxNetworks'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
    }
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            include '**/**'
            exclude '**/*.psd'
            srcDirs += 'src/generated'
            exclude '.cache'
        }
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                fluxnetworks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                fluxnetworks {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'fluxnetworks', '--all', '--output', file('src/generated/')

            mods {
                fluxnetworks {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        // location of latest mappings from the latest contents of the spreadsheet
        name 'Gigaherz'
        url 'https://www.dogforce-games.com/maven/'
    }
    maven {
        // location of the maven that hosts JEI files
        name 'Progwml6 maven'
        url 'https://dvs1.progwml6.com/files/maven/'
    }
    maven {
        // The One Probe
        name 'tterrag maven'
        url 'https://maven.tterrag.com/'
    }
    maven {
        // Curios
        name 'TheIllusiveC4 maven'
        url 'https://maven.theillusivec4.top/'
    }
    maven {
        // JEI, Mekanism
        name 'ModMaven'
        url 'https://modmaven.dev/'
    }
    maven {
        // Modern UI
        name 'IzzelAliz Maven'
        url 'https://maven.izzel.io/releases'
    }
    maven {
        // location of a maven that hosts all files on CurseForge
        name 'CurseMaven'
        url 'https://www.cursemaven.com/'
    }
    /*flatDir {
        dirs 'libs'
    }*/
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly "mekanism:Mekanism:${mekanism_version}:api"
    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}")

    compile fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}")

    compileOnly fg.deobf("mezz.jei:jei-1.16.3:${jei_version}:api")
    //runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")

    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
    //runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")

    compile fg.deobf("icyllis.modernui:ModernUI-Forge:${modernui_version}")
    compile fg.deobf("icyllis.modernui:ModernUI-Forge:${modernui_version}@pom")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Flux-Networks",
                "Specification-Vendor"    : "SonarSonic",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "SonarSonic",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
