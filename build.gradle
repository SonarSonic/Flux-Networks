plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${minecraft_version}-${mod_version}" as String
group = 'sonar.fluxnetworks'
archivesBaseName = 'FluxNetworks'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: "2023.07.16-1.20.1"

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                fluxnetworks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                fluxnetworks {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            args '--mod', 'fluxnetworks', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                fluxnetworks {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
    }
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            include '**/**'
            exclude '**/*.psd'
            srcDirs += 'src/generated/resources'
            exclude '.cache'
        }
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

repositories {
    mavenLocal()
    maven {
        // JEI
        name 'Progwml6 maven'
        url 'https://dvs1.progwml6.com/files/maven/'
    }
    maven {
        // The One Probe
        name 'tterrag maven'
        url 'https://maven.tterrag.com/'
    }
    maven {
        // Curios
        name 'TheIllusiveC4 maven'
        url 'https://maven.theillusivec4.top/'
    }
    maven {
        // JEI, Mekanism
        name 'ModMaven'
        url 'https://modmaven.dev/'
    }
    maven {
        // Modern UI
        name 'IzzelAliz Maven'
        url 'https://maven.izzel.io/releases/'
    }
    maven {
        name 'CurseMaven'
        url 'https://www.cursemaven.com/'
        content {
            includeGroup "curse.maven"
        }
    }
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    /*compileOnly "mekanism:Mekanism:${mekanism_version}:api"
    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}")*/

    implementation fg.deobf("curse.maven:the-one-probe-245211:4629624") // 1.20.1-10.0.1
    //implementation fg.deobf("curse.maven:ic2-classic-242942:4441313") // 1.19.2-2.0.5
    //runtimeOnly fg.deobf("curse.maven:powah-rearchitected-633483:3855683") // [FORGE 1.18.2] v3.0.1-beta

    //runtimeOnly fg.deobf("curse.maven:applied-energistics-2-223794:3554445")
    compileOnly fg.deobf("curse.maven:gregtechceu-modern-890405:4676506") // GregTechCEu 1.20.1-1.0.10-forge

    implementation fg.deobf("mezz.jei:jei-${minecraft_version}-core:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    compileOnly "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"

    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.2.0-beta.3+1.20.1:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.2.0-beta.3+1.20.1")

    minecraftLibrary("icyllis.modernui:ModernUI-Core:${modernui_core_version}") {
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.jetbrains", module: "annotations"
        exclude group: "com.ibm.icu", module: "icu4j"
        exclude group: "it.unimi.dsi", module: "fastutil"
    }
    minecraftLibrary("icyllis.modernui:ModernUI-Markdown:${modernui_core_version}") {
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.jetbrains", module: "annotations"
        exclude group: "com.ibm.icu", module: "icu4j"
        exclude group: "it.unimi.dsi", module: "fastutil"
    }
    implementation fg.deobf("icyllis.modernui:ModernUI-Forge:${minecraft_version}-${modernui_forge_version}")
}

processResources {
    inputs.property 'version', mod_version

    filesMatching("META-INF/mods.toml") {
        expand 'version': mod_version
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Flux-Networks",
                "Specification-Vendor"    : "SonarSonic",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "SonarSonic, BloCamLimb",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
